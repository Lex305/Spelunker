import com.modrinth.minotaur.dependencies.ModDependency
import groovy.json.JsonSlurper

plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'com.modrinth.minotaur' version '2.+'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "-" + project.mod_version_suffix
group = project.maven_group

repositories {
	maven { url "https://maven.terraformersmc.com/" }
	maven {
		url 'https://repo.maven.apache.org/maven2'
		name 'Maven Central'
	}
	maven {
		name "Siphalor's Maven"
		url "https://maven.siphalor.de/"
	}
	maven { url "https://jitpack.io" }
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	mavenLocal()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	include(modApi("de.siphalor.tweed4:tweed4-data-$project.minecraft_major_version:1.2.1+"))
	include(modApi("de.siphalor.tweed4:tweed4-data-hjson-$project.minecraft_major_version:1.1.0+"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// publishing to mod sites
task publishToModSites {
	publishToModSites.dependsOn modrinth
	publishToModSites.dependsOn curseforge
}

modrinth {
	def slurper = new JsonSlurper()

	token = System.getenv("MODRINTH_TOKEN")
	projectId = "hwl9Rw2v"
	versionNumber = project.mod_version
	versionName = "Spelunker ${project.mod_version}"
	changelog = file("CHANGELOG.md").getText("UTF-8")
	uploadFile = remapJar
	gameVersions = slurper.parseText(project.mod_game_versions)
	loaders = ["fabric"]
	dependencies = [
			new ModDependency("P7dR8mSH", "required") // Fabric API
	]
}

curseforge {
	def slurper = new JsonSlurper()
	def gameVersions = slurper.parseText(project.mod_game_versions)

	apiKey = System.getenv("CURSEFORGE_TOKEN") ?: ""
	project {
		id = "572592"
		releaseType = "release"
		changelogType = "markdown"
		changelog = file("CHANGELOG.md")

		gameVersions.each {addGameVersion it}
		addGameVersion "Java 17"
		addGameVersion "Fabric"
		relations {
			requiredDependency "fabric" // Fabric API
		}

		afterEvaluate {
			mainArtifact(remapJar) {
				displayName = "[${gameVersions[0]}] Spelunker ${project.mod_version}"
			}
			uploadTask.dependsOn(remapJar)
		}
	}
	options {
		forgeGradleIntegration = false
	}
}